plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'me.quickscythe'
version = "${plugin_version}-${build_status}-B${build_number}"

repositories {
    mavenCentral()
}

dependencies {

    testImplementation platform("org.junit:junit-bom:$junit_version")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "ch.qos.logback:logback-classic:$logback_version"

    implementation "org.json:json:$json_version"
    implementation "org.eclipse.jgit:org.eclipse.jgit:$jgit_version"
    implementation("com.zaxxer:HikariCP:$hikari_version")
}

test{
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = 'localRepo'
            url = layout.buildDirectory.dir("repo")
        }
    }
}

def targetJavaVersion = Integer.parseInt(java_version)
java {
    withSourcesJar()
    withJavadocJar()
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}
